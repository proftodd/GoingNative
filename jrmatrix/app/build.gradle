plugins {
    id 'application'
    id 'java'
}

def jniJavaVersion = JavaLanguageVersion.of(11)

java {
    toolchain {
        languageVersion = jniJavaVersion
    }
}

tasks.named("compileJava") {
    def headerDir = file("${buildDir}/generated/jni")
    options.compilerArgs += ["-h", headerDir.absolutePath]
    outputs.dir(headerDir)
}

application {
    mainClass = 'org.jtodd.jni.jrmatrix.App'
    applicationDefaultJvmArgs = [
        "-Djava.library.path=" + project(":native").layout.buildDirectory.dir("libs/shared").get().asFile.absolutePath
    ]
}

tasks.named("run") {
    dependsOn project(":native").tasks.named("linkJni")
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.guava:guava:32.1.1-jre'
    testImplementation 'junit:junit:4.13.2'
}
